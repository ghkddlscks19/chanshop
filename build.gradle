plugins {
    id 'org.springframework.boot' version '4.0.0' apply false
    id 'io.spring.dependency-management' version '1.1.7' apply false
}

group = 'com.chanshop'
version = '0.0.1-SNAPSHOT'
description = 'chanshop multi-module monolithic application'

// Common configuration for all subprojects
subprojects {
    apply plugin: 'java'
    apply plugin: 'io.spring.dependency-management'
    apply plugin: 'jacoco'

    group = 'com.chanshop'
    version = '0.0.1-SNAPSHOT'

    java {
        toolchain {
            languageVersion = JavaLanguageVersion.of(21)
        }
    }

    configurations {
        compileOnly {
            extendsFrom annotationProcessor
        }
    }

    repositories {
        mavenCentral()
    }

    dependencyManagement {
        imports {
            mavenBom "org.springframework.boot:spring-boot-dependencies:4.0.0"
        }
    }

    dependencies {
        compileOnly 'org.projectlombok:lombok'
        annotationProcessor 'org.projectlombok:lombok'
        testImplementation 'org.springframework.boot:spring-boot-starter-test'
        testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
    }

    tasks.named('test') {
        useJUnitPlatform()
        finalizedBy jacocoTestReport // 테스트 후 자동으로 리포트 생성
    }

    // Jacoco 설정
    jacoco {
        toolVersion = "0.8.12"
    }

    // Jacoco 테스트 리포트 생성
    jacocoTestReport {
        dependsOn test // 테스트 실행 후 리포트 생성

        reports {
            xml.required = true
            html.required = true
            csv.required = false
        }

        afterEvaluate {
            classDirectories.setFrom(files(classDirectories.files.collect {
                fileTree(dir: it, exclude: [
                    '**/config/**',
                    '**/dto/**',
                    '**/domain/**',
                    '**/*Application*',
                    '**/*Exception*'
                ])
            }))
        }
    }

    // Jacoco 커버리지 검증 (원하는 최소 커버리지 설정)
    jacocoTestCoverageVerification {
        violationRules {
            rule {
                enabled = true
                element = 'CLASS'

                limit {
                    counter = 'LINE'
                    value = 'COVEREDRATIO'
                    minimum = 0.70 // 70% 이상 커버리지
                }

                limit {
                    counter = 'BRANCH'
                    value = 'COVEREDRATIO'
                    minimum = 0.70
                }

                excludes = [
                    '*.config.*',
                    '*.dto.*',
                    '*.domain.*',
                    '*.*Application',
                    '*.*Exception'
                ]
            }
        }
    }
}
